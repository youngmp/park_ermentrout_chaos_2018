# wang buszaki fsu 
p i0=0.79145321756741363578785,ip=0,ton=20,toff=60
p phi=5.0
p gL=0.1
p EL=-65.0
p gNa=35.0
p ENa=55.0
p gK=9.0
p EK=-90.0
#
V'=-gL*(V-EL)-gNa*(Minf^3)*h*(V-ENa)-gK*(n^4)*(V-EK)+i(t)
h'=phi*(Hinf-h)/tauH
n'=phi*(Ninf-n)/tauN
#
#
i(t)=i0+ip*heav(t-ton)*heav(toff-t)
aux ii=i(t)
alpham = 0.1*(V+35.0)/(1.0-exp(-(V+35.0)/10.0))
betam  = 4.0*exp(-(V+60.0)/18.0)
Minf = alpham/(alpham+betam)
#
alphah = 0.07*exp(-(V+58.0)/20.0)
betah  = 1.0/(1.0+exp(-(V+28.0)/10.0))
Hinf = alphah/(alphah+betah)
tauH = 1.0/(alphah+betah)
#
alphan = 0.01*(V+34.0)/(1.0-exp(-(V+34.0)/10.00))
betan  = 0.125*exp(-(V+44.0)/80.0)
Ninf = alphan/(alphan+betan)
tauN = 1.0/(alphan+betan)
#
#
V(0)=-64
h(0)=0.78
n(0)=0.09
#
@ XP=T
@ YP=V
@ TOTAL=30
@ DT=0.01,bound=10000
@ METH=qualrk
@ TOLER=0.00001
@ XLO=0.0, XHI=30.0, YLO=-90.0, YHI=30.0
@ MAXSTOR=100000
done

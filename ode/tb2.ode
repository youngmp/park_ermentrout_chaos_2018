# 2 traub (exc) coupled to 2 wb (inh)



# slow params
par mux=1.,muy=1
par eps=.005

# coupling params (traub: it, wb: i0)
# see choose_g.nb on how these parameters are chosen

par gee=1,gei=2.1
par gie=1,gii=2.1

fFixed=.05
#itbFixed=6.03893379759
itbFixed=6.0415
iwbFixed=0.809079373711
#itbFixed=fstbInv(fFixed)
#iwbFixed=fswbInv(fFixed)

it=itbFixed-fFixed*(gee-gei)
i0=iwbFixed-fFixed*(gie-gii)


#par it=6.0965,i0=0.864079373711


# START TRB

# trb params

par ekt=-100,enat=50,elt=-67,eca=120
par glt=.2,gkt=80,gnat=100,gm=0
par ct=1
par gahp=0.5,gca=1,kd=1,alpha=.002,tauca=80,phit=4
par vshp=2.5,vlth=-25,vsshp=2,vthr=-10
# reyes set  vlth=-5,vsshp=10
par betas=.1,alphas=2
par vwt=-35,tauw=100
#aux iahp=i_ahp
#aux im=i_m


# Trb 1
itrb(v,m,h,n)=gnat*h*m^3*(v-enat)+(gkt*n^4)*(v-ekt)+glt*(v-elt)
vt'=-(itrb(vt,mt,ht,nt) -(it+gee*str)+i_ca(vt)+i_ahp(ca,vt)+i_m(vt,wt))/ct
mt'=am(vt)*(1-mt)-bm(vt)*mt
nt'=an(vt)*(1-nt)-bn(vt)*nt
ht'=ah(vt)*(1-ht)-bh(vt)*ht
wt'=(winf(vt)-wt)/tw(vt)
st'=alphas*(1-st)/(1+exp(-(vt-vthr)/vsshp))-betas*st

# calcium
ca'=(-alpha*i_ca(vt)-ca/tauca)


# Trb 2
vtp'=-(itrb(vtp,mtp,htp,ntp)-(it+gee*str)+i_ca(vtp)+i_ahp(cap,vtp)+i_m(vtp,wtp))/ct
mtp'=am(vtp)*(1-mtp)-bm(vtp)*mtp
ntp'=an(vtp)*(1-ntp)-bn(vtp)*ntp
htp'=ah(vtp)*(1-htp)-bh(vtp)*htp
wtp'=(winf(vtp)-wtp)/tw(vtp)
stp'=alphas*(1-stp)/(1+exp(-(vtp-vthr)/vsshp))-betas*stp

# trb calcium
cap'=(-alpha*i_ca(vtp)-cap/tauca)

# trb functions
am(v)=.32*(54+v)/(1-exp(-(v+54)/4))
bm(v)=.28*(v+27)/(exp((v+27)/5)-1)
ah(v)=.128*exp(-(50+v)/18)
bh(v)=4/(1+exp(-(v+27)/5))
an(v)=.032*(v+52)/(1-exp(-(v+52)/5))
bn(v)=.5*exp(-(57+v)/40)

# trb time fns
TW(vs)=tauw/(3.3*EXP((vs-vwt)/20.0)+EXP(-(vs-vwt)/20.0))
WINF(vs)=1.0/(1.0+EXP(-(vs-vwt)/10.0))

# trb calcium functions
mlinf(vt)=1/(1+exp(-(vt-vlth)/vshp))
i_ca(vt)=gca*mlinf(vt)*(vt-eca)
i_ahp(ca,vt)=gahp*(ca/(ca+kd))*(vt-ekt)
i_m(vt,wt)=gm*wt*(vt-ekt)


# END TRB

# BEGIN SX,SY (STR, SWB)
str'=eps*(-str)/mux

global 1 vt-0 {str=str+eps/(2*mux)}
global 1 vtp-0 {str=str+eps/(2*mux)}

# preliminary calculations in big nb#1 pg 47

# TRB init
init vtp=-90,mtp=0,ntp=0.2,htp=.8,wtp=.3,stp=.5,cap=.2,vt=42.68904,mt=.9935,nt=.4645,ht=.47785,wt=.268,st=.2917,ca=.294

# slow init
init str=.05

#
@ XP=T
@ YP=Vt
@ TOTAL=1000
@ DT=0.01,bound=10000
@ METH=qualrk
@ TOLER=0.00001
@ MAXSTOR=100000
@ XLO=0.0, XHI=30.0, YLO=-90.0, YHI=30.0
done

# old functions
#alphah = 0.07*exp(-(V+58.0)/20.0)
#betah  = 1.0/(1.0+exp(-(V+28.0)/10.0))
#Hinf = alphah/(alphah+betah)
#tauH = 1.0/(alphah+betah)

#alpham = 0.1*(v+35.0)/(1.0-exp(-(v+35.0)/10.0))
#betam  = 4.0*exp(-(v+60.0)/18.0)
#Minf = alpham/(alpham+betam)

#alphan(v) = 0.01*(V+34.0)/(1.0-exp(-(V+34.0)/10.00))
#betan  = 0.125*exp(-(V+44.0)/80.0)
#Ninf = alphan/(alphan+betan)
#tauN = 1.0/(alphan+betan)

